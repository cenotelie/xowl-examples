PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xowl: <http://xowl.org/infra/denotation/schema#>
PREFIX kernel:   <http://xowl.org/platform/schemas/kernel#>
PREFIX syseng:   <http://holons-hub.com/collab/schemas/syseng#>
PREFIX artifact: <http://holons-hub.com/collab/artifact/>
BASE             <http://holons-hub.com/collab/artifact/>

RULE "Diagram is the system's state machine"
	SIGN ?diagram
		WITH xowl:shape = "StateMachine_Shape"
IS
	A SEME syseng:System ID (artifact:System "#" ?diagram.xowl:name) AS ?system
		WITH kernel:name = ?diagram.xowl:name
		WITH syseng:hasBehavior = ?sm
	AND A SEME syseng:StateMachine AS ?sm
		BIND TO SIGN ?diagram


RULE "State shapes are states of the state machine"
	SIGN ?sign
		WITH xowl:shape = "State_Shape"
IS
	A SEME syseng:State ID (artifact:State "#" ?sign.xowl:name) AS ?state
		WITH kernel:name = ?sign.xowl:name
		BIND TO SIGN ?sign


RULE "Arrows are Transitions between States triggered by System Event"
	SIGN ?arrow
		WITH xowl:shape = "Transition_Edge"
		RELATION xowl:from ?signFrom
		RELATION xowl:to ?signTo
	AND SIGN ?signFrom
		BOUND TO SEME ?stateOrigin
	AND SIGN ?signTo
		BOUND TO SEME ?stateTarget
IS
	A SEME syseng:SystemEvent ID (artifact:SystemEvent "#" ?arrow.xowl:name) AS ?event
		WITH kernel:name = ?arrow.xowl:name
	AND A SEME syseng:Transition
		WITH syseng:transitionSymbol = ?event
		WITH syseng:transitionFrom = ?stateOrigin
		WITH syseng:transitionTo = ?stateTarget
		BIND TO SIGN ?arrow


RULE "Pseudo-state initial state is the State Machine's Initial state"
	SIGN ?signInit
		WITH xowl:shape = "Pseudostate_InitialShape"
	AND SIGN ?arrow
		WITH xowl:shape = "Transition_Edge"
		RELATION xowl:from ?signInit
		RELATION xowl:to ?signState
	AND SIGN ?signState
		WITH xowl:shape = "State_Shape"
		BOUND TO SEME ?state
	AND SIGN ?signDiagram
		WITH xowl:shape = "StateMachine_Shape"
		BOUND TO SEME ?sm
IS
	SEME ?sm
		WITH syseng:initState = ?state